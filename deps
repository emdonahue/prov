#!/bin/zsh

#TODO make edit escape backslashes so they dont end up as ctrl chars
#TODO make edit retarget the recipe relative to pwd
#TODO escape sed patterns
#TODO make add with no arguments accept recipe on stdin
#TODO make cp handle directory structures. currently only works for simple, flat cases.
#TODO echo should escape backslashes
#TODO cp should delete overwritten target
#TODO convert sedpattern/sedreplacement to functions

function deps() {

    local US=$'\037'
    local SEDPATTERN='s/[^^]/[&]/g; s/\^/\\^/g'
    local SEDREPLACEMENT='s/[&/\]/\\&/g'

    function deps__path() {
	realpath -ms --relative-to="$MAKEDIR" "$@"
    }

    function deps__pattern() {
	sed "$SEDPATTERN" <<< "$*"
    }

    function deps__replacement() {
	sed "$SEDREPLACEMENT" <<< "$*"
    }

    function deps__replace_filename() { #OLDRELATIVE NEWABSOLUTE (recipe on stdin or 3 if inplace)
	sed ${3:+-i} 's/\(^\|[><| ]\)'"$(sed "$SEDPATTERN" <<< "$1")"'\([><:/|) ]\|$\)/\1'"$(sed "$SEDREPLACEMENT" <<< "$(deps__path "$2")")"'\2/g' "${3:--}"
    }

    function deps__awk() { #[ARG...] TARGET PROGRAM
	awk -vTARGET="$(sed $SEDPATTERN <<< "$@[-2]")" $@[1,-3] '!/^\t/ {rule=0} $0 ~ "^([^\t][^:]* )?"TARGET"[ :/]" && $0 !~ "^all:|^clean:" {rule=1} '"$@[-1]" "$MAKEFILE"
    }

    function deps__delete_rule() {
	deps__awk -i inplace "$1" '!rule'
    }
    
    function deps__edit_rule() { #TARGET PATTERN REPLACEMENT GUARD
	deps__awk -vPATTERN="${2//\\/\\\\}" -vREPLACEMENT="${3//\\/\\\\}" -i inplace "$1" 'rule && '"${4:-1}"' {print gensub(PATTERN,REPLACEMENT,"g")} !rule || !'"${4:-1}"
    }
    
    function deps__echo_rule() { #TARGET
	deps__awk "$1" 'rule'
    }

    local CMD="$1"
    [[ $# -ne 0 ]] && shift

    # Find Makefile for subcommands that require it
    case "$CMD" in
	-h|--help|help|init|'');;
	*)
	    local MAKEFILE=$(DIR=$(pwd); while [[ -n "$DIR" && ! -f "$DIR/Makefile" ]]; do DIR="${DIR%/*}"; done; echo "${DIR}/Makefile")
	    [[ ! -f "$MAKEFILE" ]] && echo "Makefile not found. Use $(basename "$0") init." 1>&2 && return 1
	    ;;
    esac

    local MAKEDIR="$(dirname "$MAKEFILE")"

    # Parse the main subcommand
    case "$CMD" in
	--help|-h|help) #[SUBCOMMAND], Prints help text and options for SUBCOMMAND. If SUBCOMMAND omitted, prints list of subcommands.
	    [[ $# -eq 0 ]] && echo 'deps - Automatic Makefile generation for data processing pipelines from commandline history\nUsage: deps SUBCOMMAND [ARGUMENTS...]\nSubcommands:'
	    grep -E -- "${1:-\\w+}\)\s#" "$functions_source[$0]" | sed -E -- "s/^[[:space:]]*([-|[:alpha:]]+)\)\s#([^,]*), (.*)/\1 \2;\3/" | sed $((${+1}+1))',$ s/^[[:space:]]*/\t/' | column -t -s\;
	    ;;

	add) #[FILE...], Heuristically scans history for recipe that created FILE and adds to Makefile. Recipes must contain redirects to FILE (eg "> FILE"). Dependencies are determined by tokens contained in recipe that refer to current files. Must be run from directory where command was first executed.
	    for f in "$@"; do
		local RECIPE=$(fc -nrl -1000 | grep -E ">\|?\s*$f([ )(><|]+|$)" | head -n1)
		if [[ -z "$RECIPE" ]]; then
		    echo "No recipe found for $f" >&2
		else
		    local -U DEPENDENCIES=()
		    local IFS=$IFS')(><|'
		    for D in "${=RECIPE}"; do
			if [[ -f "$D" && "$D" != "$f" ]]; then
			    RECIPE=$(deps__replace_filename "$D" "$D" <<< "$RECIPE")
			    DEPENDENCIES+=("$(deps__path "$D")")
			fi
		    done
		    RECIPE=$(deps__replace_filename "$f" "$f" <<< "$RECIPE")
		    "$0" set "$(deps__path "$f")" "$DEPENDENCIES" <<< "$RECIPE"
		    echo "Recipe added to $MAKEFILE:"
		    "$0" echo "$f"
		fi
	    done
	    ;;

	comment) #TARGET [COMMENT], Sets or prints a short comment associated with the recipe for TARGET. Useful for quick notes annotating generated files. If COMMENT is present, set the comment, otherwise print.
	    if [[ $# -eq 0 ]]; then
		"$0" help "$CMD" 1>&2 && return 1
	    elif [[ $# -eq 1 ]]; then
		grep -E '^'"$(deps__path "$1")"':' "$MAKEFILE" | sed -E 's/^[^#]*#?//'
	    else
		local TARGET="${$(deps__path "$1"):gs_/_\\/_}"
		shift
		sed -Ei -- "/^$TARGET:/ s/([^#]*)#?.*/\1#${*}/" "$MAKEFILE"
	    fi
	    ;;

	cp) #SOURCE... DEST, Recursively copies the SOURCE files and folders to DEST, modifying their mutual dependencies to point to the new copies within a given cp command.
	    if [[ ! -d "${@[-1]}" ]]; then local CP_BASENAME=; else unset CP_BASENAME; fi #if file->dir use file basename2
	    cp -ir "$@"
	    local CP_SRCFILES=(${(u)${(@0):-"$(realpath -sz --relative-to "$MAKEDIR" "${@[1,-2]}")"}})	    
	    for target in "${CP_SRCFILES[@]}"; do
		local RECIPE=$(deps__echo_rule "$target")
		for retarget in "${CP_SRCFILES[@]}"; do
		    RECIPE=$(deps__replace_filename "$retarget" "${@[-1]}${CP_BASENAME-/$(basename "$retarget")}" <<< "$RECIPE")
		done
		echo "$RECIPE" >> "$MAKEFILE"
	    done
	    ;;

	echo) #TARGET, Echoes the recipe for TARGET
	    [[ ! -f "$MAKEFILE" ]] && return 1
	    local RECIPE=$(deps__echo_rule $(deps__path "$1") | sed 's/\$\$/$/g')
	    [[ -z "$RECIPE" ]] && echo "No recipe for $1 found in $MAKEFILE" >&2 && return 1
	    echo "$RECIPE"
	    ;;

	edit) #[TARGET], Places the recipe for TARGET on the command line so it can be modified and re-added to the Makefile if needed. If TARGET is omitted, open EDITOR on the Makefile.
	    if [[ $1 ]]; then
		print -z "$("$0" echo "$1" | tail -n +2 | sed -E 's/^\s*//')"
	    else
		${=EDITOR} "$MAKEFILE"
	    fi
	    ;;

	init) #, Creates a new Makefile in the current directory. Use before other commands that attempt to modify the Makefile.
	    [[ -f Makefile ]] && {echo 'Makefile already exists' 1>&2; return 1}
	    echo 'SHELL=zsh' >> Makefile
	    echo '.PHONY: all clean' >> Makefile
	    echo -E 'TARGETS=$(shell sed -nE "/^clean:/,$$ {/^[^\t].*:/ s/:.*//p}" Makefile | tr -s " " "\n" | grep -Zvx -e "all" -e "clean" -e "^\.PHONY" | tee >(xargs -r dirname -z | xargs -r0 mkdir -p) | tr \\0 " ")' >> Makefile
	    echo 'all: $(TARGETS)' >> Makefile
	    echo 'clean:' >> Makefile
	    echo '\trm -f $(TARGETS)' >> Makefile
	    ;;

	less) #, Opens the Makefile in less.
	    less "$MAKEFILE"
	    ;;

	make) #, Runs the nearest ancestor Makefile.
	    deps__path "$@" | xargs make -C "$MAKEDIR"
	    ;;

	merge) #TARGET [PATTERN...], Converts the existing rule for TARGET into a generic recipe and adds PATTERN as a parallel target to be built using the same instructions. By default, > TARGET will be replaced with $@ and substrings of TARGET separated by - will be replaced with code to extract the relevant portion from each PATTERN including TARGET. Each PATTERN & TARGET should be of the form a-b-c... where each component is one argument needed to define the recipe.
	    () {
		deps__edit_rule "$1" '>\s*'"$(deps__pattern $1)"'\>' '> $@' '/^\t/'
		local ARGS=(${(s:-:)1:r:t})
		for i in {1..$#ARGS}; do
		    deps__edit_rule "$1" '\<'"$(deps__pattern $ARGS[$i])"'\>' '$(words '$i',$(subst -, ,$(@F)))' '/^\t/'
		done
		deps__edit_rule "$1" '^([^:]*):' '\1'${@[2]:+ }"$*[2,-1]:" '!/^\t/'
	    } "${(@f)$(deps__path "$@")}"
	    ;;
	
	mv) #SOURCE... DEST, Moves a file with mv and updates the Makefile accordingly.
	    if [[ -d "${@[-1]}" ]]; then
		for f in "${@[1,-2]}"; do
		    "$0" mv "$f" "${@[-1]}/$(basename $f)"
		done
	    else
		mv -i "$@"
		"$0" unset "$(deps__path "$2")" #Delete overwritten recipe if it exists.
		deps__replace_filename "$(deps__path "$1")" "$2" "$MAKEFILE"
	    fi
	    ;;

	rm) #TARGET, Unsets the recipe for building TARGET from the Makefile and deletes the corresponding file.
	    for f in "$@"; do
		"$0" unset "$(deps__path "$f")"
	    done
	    rm -rf "$@"
	    ;;

	set) #TARGET [DEPENDENCY...], Creates a new recipe for TARGET using DEPENDENCY and a recipe from stdin.
    	    "$0" unset "$1"
	    echo -n "$1: " >> "$MAKEFILE"
	    shift
	    echo "$@" >> "$MAKEFILE"
	    <&0 sed -e 's/^/\t/' -e 's/\$/$$/g' >> "$MAKEFILE"
	    ;;

	unset) #TARGET, Deletes the recipe for building TARGET from the Makefile.
	    for t in "$@"; do
		deps__delete_rule "$t"
	    done
	    ;;

	*)
	    "$0" help 1>&2
	    return 1
	    ;;
    esac
}
